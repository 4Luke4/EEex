BACKUP ~weidu_external/EEex/backup~
AUTHOR ~Bubb~
VERSION ~devel~
README ~EEex/readme-EEex.html~

QUICK_MENU
	ALWAYS_ASK
	END
	////////////////////////////////////////////////////////////////////////////////
	// Minimal      | Engine fixes and core functionality required by other mods. //
	////////////////////////////////////////////////////////////////////////////////
	@3000 BEGIN
		1 // B3-EEex-Main
	END
	//////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Full         | "Minimal" + All optional modules enabled by default. Modules can be disabled in-game. //
	//////////////////////////////////////////////////////////////////////////////////////////////////////////
	@3001 BEGIN
		1 // B3-EEex-Main
		2 // B3-EEex-Module-Effect-Menu
		3 // B3-EEex-Module-Empty-Containers
		4 // B3-EEex-Module-Hotkey
		5 // B3-EEex-Module-Scale
		6 // B3-EEex-Module-Time-Step
		7 // B3-EEex-Module-Timer-Main
	END
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	// Experimental | "Full"    + All experimental components. WARNING: Only install if you understand the risks. //
	////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	@3002 BEGIN
		1 // B3-EEex-Main
		2 // B3-EEex-Module-Effect-Menu
		3 // B3-EEex-Module-Empty-Containers
		4 // B3-EEex-Module-Hotkey
		5 // B3-EEex-Module-Scale
		6 // B3-EEex-Module-Time-Step
		7 // B3-EEex-Module-Timer-Main
		8 // B3-EEex-LuaJIT
	END
END

ALWAYS
	ACTION_IF !VARIABLE_IS_SET EEex_always THEN BEGIN
		INCLUDE ~EEex/lib/always.tph~
		OUTER_SET EEex_always = 1
	END
END

AUTO_TRA ~EEex/language/%s~
LANGUAGE ~English~ ~english~ ~EEex/language/english/setup.tra~

/////////////////////
// Quick Menu Core //
/////////////////////

BEGIN @0

//////////
// EEex //
//////////

BEGIN @1 LABEL ~B3-EEex-Main~ DESIGNATED 1
REQUIRE_PREDICATE (GAME_IS ~bgee bg2ee eet iwdee~ AND FILE_EXISTS ~data/PATCH26.BIF~) @1000 // Game not supported.
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ 0 @1001 // This component requires the core component to be installed.

	ACTION_FOR_EACH file IN ACTION OBJECT TRIGGER BEGIN
		COPY ~EEex/patch/%file%.IDS~ ~EEex/patch~
			COUNT_2DA_ROWS 2 rows
			FOR (i = 0; i <= rows; ++i) BEGIN
				READ_2DA_ENTRY i 0 2 "col1"
				READ_2DA_ENTRY i 1 2 "col2"
				DEFINE_ASSOCIATIVE_ARRAY EEex_append_array BEGIN ~%file%~ , ~%col1%~ , ~%col2%~ => ~~ END
			END
		BUT_ONLY
	END

	DEFINE_ACTION_FUNCTION B3_ESCAPE_STRING
		STR_VAR
			str = ~~
		RET
			escaped_str
	BEGIN
		ACTION_DEFINE_ASSOCIATIVE_ARRAY to_escape BEGIN ~$~ => 1 ~^~ => 1 ~.~ => 1 ~*~ => 1 ~+~ => 1 ~?~ => 1 ~[~ => 1 ~]~ => 1 ~\~ => 1 END
		OUTER_PATCH_SAVE escaped_str ~%str%~ BEGIN
			limit = BUFFER_LENGTH
			FOR (i = 0; i < limit; ++i) BEGIN
				READ_ASCII i character (1)
				PATCH_IF VARIABLE_IS_SET EVAL ~to_escape_%character%~ BEGIN
					INSERT_BYTES i 1
					WRITE_ASCII i ~\~
					++i
					++limit
				END
			END
		END
	END

	ACTION_PHP_EACH EEex_append_array AS data => ~~ BEGIN
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_1%~ RET data_1_escaped = escaped_str END
		LAF B3_ESCAPE_STRING STR_VAR str = EVAL ~%data_2%~ RET data_2_escaped = escaped_str END
		APPEND ~%data%.IDS~ ~%data_1% %data_2%~ UNLESS ~^[ %TAB%]*%data_1_escaped%[ %TAB%]+%data_2_escaped%[ %TAB%]*%MNL%*$~
	END

	COPY ~EEex/loader~ ~.~
	COPY ~EEex/loader/Lua52~ ~.~
	COPY ~EEex/copy/EEex_scripts~ ~EEex_scripts~
	COPY ~EEex/copy/override~ ~override~

//////////////////////////////////////////////////////////////////////////////////
// Enable effect menu module: LShift-on-hover to view spells affecting creature //
//////////////////////////////////////////////////////////////////////////////////

BEGIN @2 LABEL ~B3-EEex-Module-Effect-Menu~ DESIGNATED 2
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Effect Menu Module~ key=~Enable~ value=~1~ END
	BUT_ONLY

///////////////////////////////////////////////////////////////////////////////////////
// Enable empty container module: Highlight empty containers in gray instead of cyan //
///////////////////////////////////////////////////////////////////////////////////////

BEGIN @3 LABEL ~B3-EEex-Module-Empty-Containers~ DESIGNATED 3
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Empty Container Module~ key=~Enable~ value=~1~ END
	BUT_ONLY

///////////////////////////////////////////////////////////////////////////////////////
// Enable hotkey module: Edit override/B3Hotkey.lua to create advanced spell hotkeys //
///////////////////////////////////////////////////////////////////////////////////////

BEGIN @4 LABEL ~B3-EEex-Module-Hotkey~ DESIGNATED 4
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex_scripts/EEex_Modules.lua~ ~EEex_scripts~
		REPLACE_TEXTUALLY ~^\([ %TAB%]+\["B3Hotkey"\][ %TAB%]+=[ %TAB%]+\)false~ ~\1true~
	BUT_ONLY

/////////////////////////////////////////////////////////
// Enable scale module: Customizable UI scaling factor //
/////////////////////////////////////////////////////////

BEGIN @5 LABEL ~B3-EEex-Module-Scale~ DESIGNATED 5
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Scale Module~ key=~Enable~ value=~1~ END
	BUT_ONLY

//////////////////////////////////////////////////////////////
// Enable time step module: Advance 1 game tick on keypress //
//////////////////////////////////////////////////////////////

BEGIN @6 LABEL ~B3-EEex-Module-Time-Step~ DESIGNATED 6
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Time Step Module~ key=~Enable~ value=~1~ END
	BUT_ONLY

///////////////////////////////////////////////////////////////////////////////////////////////////////
// Enable timer module: Visual indicators for modal actions, contingencies, and spell/item cooldowns //
///////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @7 LABEL ~B3-EEex-Module-Timer-Main~ DESIGNATED 7
GROUP @2000 // Optional
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~EEex.ini~ ~.~
		LPF SetINIString STR_VAR section=~Timer Module~ key=~Enable~ value=~1~ END
	BUT_ONLY

/////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Experimental - Use LuaJIT: Can help stuttering | WARNING: May cause crashes when combined with certain mods //
/////////////////////////////////////////////////////////////////////////////////////////////////////////////////

BEGIN @8 LABEL ~B3-EEex-LuaJIT~ DESIGNATED 8
GROUP @2001 // Experimental | WARNING: Only install if you understand the risks
REQUIRE_PREDICATE MOD_IS_INSTALLED ~EEex.tp2~ (ID_OF_LABEL ~EEex.tp2~ ~B3-EEex-Main~) @1002 // This component requires the main component to be installed.

	COPY ~InfinityLoader.ini~ ~.~
		REPLACE_TEXTUALLY ~^\(LuaLibrary=\).*~ ~\1lua51.dll~
		REPLACE_TEXTUALLY ~^\(LuaPatchMode=\).*~ ~\1REPLACE_INTERNAL_WITH_EXTERNAL~
		REPLACE_TEXTUALLY ~^\(LuaVersionExternal=\).*~ ~\15.1~
	BUT_ONLY

	COPY ~EEex/loader/LuaJIT~ ~.~
